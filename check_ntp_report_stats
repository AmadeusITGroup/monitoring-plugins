#!/usr/bin/env perl

# check_ntp_amadeus - Check reachability of an NTP server. Provides much performance data
#
# Copyright (c) 2015 Amadeus Germany GmbH
#
# License: MIT
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;

use IPC::Open3;

use Nagios::Plugin;

my $np = Nagios::Plugin->new(
	usage => "Usage: %s [ -v|--verbose ] ",
	version => '1.0.0',
	plugin => 'check_ntp_amadeus',
	blurb => 'Check NTP stats',
);

$np->add_arg(
	spec => 'host|h=s',
	help => [
		'NTP Server to query',
	],
	default => undef,
);

$np->getopts;

my($wtr, $rdr, $err);
use Symbol 'gensym';
$err = gensym;

my $pid = open3($wtr, $rdr, $err, 'ntpdc -c sysinfo');

waitpid($pid, 0);
my $child_exit_status = $? >> 8;

if ($child_exit_status != 0) {
  $np->nagios_exit(CRITICAL, "Execution returned $child_exit_status");
}

my $stderr = <$err>;

if (defined($stderr)) {
  $np->nagios_exit(CRITICAL, "Output from plugin: $stderr");
}

my %response;

# Here is a sample output of the ntpdc -c sysinfo call:
#
# system peer:          ntp8.kashra-server.com
# system peer mode:     client
# leap indicator:       00
# stratum:              2
# precision:            -20
# root distance:        0.04266 s
# root dispersion:      0.04320 s
# reference ID:         [62.75.254.179]
# reference time:       d9d8a1f5.fef40b19  Mon, Oct 26 2015 12:58:29.995
# system flags:         auth monitor ntp kernel stats
# jitter:               0.002823 s
# stability:            0.000 ppm
# broadcastdelay:       0.000000 s
# authdelay:            0.000000 s

while (<$rdr>) {
  my ($k, $v) = split(':', $_, 2);
  $k =~ s/^\s+|\s+$//g ;
  $v =~ s/^\s+|\s+$//g ;
  $v =~ s/ (s|ppm)$//g ;
  $response{$k} = $v;
}

$np->add_perfdata(
  label => 'stratum',
  value => $response{'stratum'},
);

$np->add_perfdata(
  label => 'root_distance',
  value => $response{'root distance'},
  uom   => 's',
);

$np->add_perfdata(
  label => 'root_dispersion',
  value => $response{'root dispersion'},
  uom   => 's',
);

$np->add_perfdata(
  label => 'precision',
  value => $response{'precision'},
);

$np->add_perfdata(
  label => 'jitter',
  value => $response{'jitter'},
  uom   => 's',
);

$np->add_perfdata(
  label => 'stability',
  value => $response{'stability'},
);

$np->nagios_exit(OK, 'Connection to NTP server successful');
