#!/usr/bin/env perl

# check_systemd_units - Check that no systemd units have failed
#
# Copyright (c) 2015 Amadeus Germany GmbH
#
# License: MIT
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;

use autodie;

### START PLUGIN LOADER
sub load_module {
	my $module;
	eval "require $_" and $module = $_ and last for @_;
	$module->import();
	return $module;
}

my $plugin_module;
BEGIN {
	$plugin_module = load_module('Monitoring::Plugin', 'Nagios::Plugin', 'Nagios::Monitoring::Plugin');
}
### END PLUGIN LOADER

my $np = $plugin_module->new(
	usage => "Usage: %s [ -v|--verbose ] "
	. "[ -c|--critical=<threshold> ] [ -w|--warning=<threshold> ]",
	version => '1.0.0',
	plugin => 'check_systemd_units',
	blurb => 'Check failed systemd units',
);

$np->add_arg(
	spec => 'warning|w=i',
	help => [
		'Exit with WARNING status if more than INTEGER units are failed',
	],
	default => '0',
);

$np->add_arg(
	spec => 'critical|c=i',
	help => [
		'Exit with CRITICAL status if more than INTEGER units are failed',
	],
	default => '0',
);

$np->getopts;

sub collect_output {
	my ($program, @args) = @_;

	# Opening a command as a safe no-shell run
	open(RUNIT, "-|", $program, @args);
	my @content = <RUNIT>;
    close(RUNIT);
	return @content;
}

my $warning_threshold  = $np->opts->warning;
my $critical_threshold = $np->opts->critical;

my @failed_output = collect_output("systemctl", ("list-units", "--no-legend", "--failed"));
my $total = scalar(collect_output("systemctl", ("list-units", "--no-legend")));

# the systemctl list-units command returns 5 columns:
#
# <NAME>  <STATUS>  <ACTIVE>  <STATE>  <DESCRIPTION>
#
# Here is a sample output:
#
# netfilter-persistent.service       loaded active exited  netfilter persistent configuration
# networking.service                 loaded active running LSB: Raise network interfaces.
# ntp.service                        loaded active running Network Time Service
# postfix.service                    loaded active running Postfix Mail Transport Agent
# puppet.service                     loaded active running Puppet agent
# rc-local.service                   loaded active exited  /etc/rc.local Compatibility

my @failed = map { (split(/\s+/, $_))[0] } @failed_output;

$np->add_perfdata(
	label => "failed",
	value => scalar(@failed),
	warning => $warning_threshold,
	critical => $critical_threshold,
);

$np->add_perfdata(
	label => "total",
	value => $total,
);

my $code = $np->check_threshold(
	check => scalar(@failed),
	warning => $warning_threshold,
	critical => $critical_threshold,
);
my $message = sprintf("%d failed: %s", scalar(@failed), join(' ', @failed));
$np->nagios_exit($code, $message);
